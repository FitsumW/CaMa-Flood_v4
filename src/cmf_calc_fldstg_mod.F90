MODULE CMF_CALC_FLDSTG_MOD
!==========================================================
!* PURPOSE: calculate river and floodplain staging
!
! (C) D.Yamazaki & E. Dutra  (U-Tokyo/FCUL)  Aug 2019
!
! Licensed under the Apache License, Version 2.0 (the "License");
!   You may not use this file except in compliance with the License.
!   You may obtain a copy of the License at: http://www.apache.org/licenses/LICENSE-2.0
!
! Unless required by applicable law or agreed to in writing, software distributed under the License is 
!  distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
! See the License for the specific language governing permissions and limitations under the License.
!==========================================================
CONTAINS
!####################################################################
! -- CMF_CALC_FLDSTG_DEF  !! default flood stage calculation
! -- CMF_OPT_FLDSTG_ES    !! optimized code for vector processor (such as Earth Simulator), activated using LSTG_ES=.TRUE. option).
! --
!####################################################################
SUBROUTINE CMF_CALC_FLDSTG_DEF
USE PARKIND1,           ONLY: JPIM, JPRB, JPRD
USE YOS_CMF_INPUT,      ONLY: NLFP
USE YOS_CMF_MAP,        ONLY: NSEQALL, B2GRAREA, B2RIVLEN, B2RIVWTH, B2RIVELV
USE YOS_CMF_MAP,        ONLY: B2RIVSTOMAX, B2FLDSTOMAX, B2FLDGRD, BFRCINC
USE YOS_CMF_PROG,       ONLY: D2RIVSTO, D2FLDSTO
USE YOS_CMF_DIAG,       ONLY: B2RIVDPH, B2FLDDPH, B2FLDFRC, B2FLDARE, B2SFCELV
USE YOS_CMF_DIAG,       ONLY: DGLBSTOPRE2, DGLBSTONEW2, DGLBRIVSTO, DGLBFLDSTO, DGLBFLDARE
IMPLICIT NONE

!*** LOCAL
INTEGER(KIND=JPIM),SAVE    :: ISEQ, I
REAL(KIND=JPRD),SAVE       :: DSTOALL, DSTONOW, DSTOPRE, DWTHNOW, DWTHPRE, DDPHPRE, DWTHINC
!$OMP THREADPRIVATE        (I,DSTOALL, DSTONOW, DSTOPRE, DWTHNOW, DWTHPRE, DDPHPRE, DWTHINC)
!================================================
DGLBSTOPRE2=0._JPRD
DGLBSTONEW2=0._JPRD
DGLBRIVSTO =0._JPRD
DGLBFLDSTO =0._JPRD
DGLBFLDARE =0._JPRD

! Estimate water depth and flood extent from water storage
!   Solution for Equations (1) and (2) in [Yamazaki et al. 2011 WRR].

!$OMP PARALLEL DO REDUCTION(+:DGLBSTOPRE2,DGLBSTONEW2,DGLBRIVSTO,DGLBFLDSTO,DGLBFLDARE)
DO ISEQ=1, NSEQALL
!
  DSTOALL = D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)

  IF( DSTOALL > B2RIVSTOMAX(ISEQ,1) )THEN
    I=1
    DSTOPRE = B2RIVSTOMAX(ISEQ,1)
    DWTHPRE = B2RIVWTH(ISEQ,1)
    DDPHPRE = 0._JPRB
    DWTHINC = B2GRAREA(ISEQ,1) * B2RIVLEN(ISEQ,1)**(-1.) * BFRCINC
    DO WHILE( DSTOALL > B2FLDSTOMAX(ISEQ,1,I) .AND. I<=NLFP)
      DSTOPRE = B2FLDSTOMAX(ISEQ,1,I)
      DWTHPRE = DWTHPRE + DWTHINC
      DDPHPRE = DDPHPRE + B2FLDGRD(ISEQ,1,I) * DWTHINC
      I=I+1
      IF( I>NLFP ) EXIT
    END DO
    IF( I>NLFP )THEN
      DSTONOW = DSTOALL - DSTOPRE
      DWTHNOW = 0._JPRB
      B2FLDDPH(ISEQ,1) = DDPHPRE + DSTONOW * DWTHPRE**(-1.) * B2RIVLEN(ISEQ,1)**(-1.)
    ELSE
      DSTONOW =  DSTOALL - DSTOPRE
      DWTHNOW = -DWTHPRE + &
&      ( DWTHPRE**2. + 2._JPRB * DSTONOW * B2RIVLEN(ISEQ,1)**(-1.) * B2FLDGRD(ISEQ,1,I)**(-1.) )**0.5
      B2FLDDPH(ISEQ,1) = DDPHPRE + B2FLDGRD(ISEQ,1,I) * DWTHNOW
    ENDIF
    D2RIVSTO(ISEQ,1) = B2RIVSTOMAX(ISEQ,1) + B2RIVLEN(ISEQ,1) * B2RIVWTH(ISEQ,1) * B2FLDDPH(ISEQ,1)
    D2RIVSTO(ISEQ,1) = MIN(D2RIVSTO(ISEQ,1),DSTOALL)

    B2RIVDPH(ISEQ,1) = D2RIVSTO(ISEQ,1) * B2RIVLEN(ISEQ,1)**(-1.) * B2RIVWTH(ISEQ,1)**(-1.)
!
    D2FLDSTO(ISEQ,1) = DSTOALL - D2RIVSTO(ISEQ,1)
    D2FLDSTO(ISEQ,1) = MAX( D2FLDSTO(ISEQ,1), 0._JPRD )
    B2FLDFRC(ISEQ,1) = (-B2RIVWTH(ISEQ,1) + DWTHPRE + DWTHNOW ) * (DWTHINC*NLFP)**(-1.)  !! bugfix 191113, (10._JPRB -> NLFP)
    B2FLDFRC(ISEQ,1) = MAX( B2FLDFRC(ISEQ,1),0._JPRB)
    B2FLDFRC(ISEQ,1) = MIN( B2FLDFRC(ISEQ,1),1._JPRB)
    B2FLDARE(ISEQ,1) = B2GRAREA(ISEQ,1)*B2FLDFRC(ISEQ,1)
  ELSE
    D2RIVSTO(ISEQ,1) = DSTOALL
    B2RIVDPH(ISEQ,1) = DSTOALL * B2RIVLEN(ISEQ,1)**(-1.) * B2RIVWTH(ISEQ,1)**(-1.)
    B2RIVDPH(ISEQ,1) = MAX( B2RIVDPH(ISEQ,1), 0._JPRB )
    D2FLDSTO(ISEQ,1) = 0._JPRD
    B2FLDDPH(ISEQ,1) = 0._JPRB
    B2FLDFRC(ISEQ,1) = 0._JPRB
    B2FLDARE(ISEQ,1) = 0._JPRB
  ENDIF
  B2SFCELV(ISEQ,1)     = B2RIVELV(ISEQ,1) + B2RIVDPH(ISEQ,1)

  DGLBSTOPRE2     = DGLBSTOPRE2 + DSTOALL
  DGLBSTONEW2     = DGLBSTONEW2 + D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)
  DGLBRIVSTO      = DGLBRIVSTO  + D2RIVSTO(ISEQ,1)
  DGLBFLDSTO      = DGLBFLDSTO  + D2FLDSTO(ISEQ,1)
  DGLBFLDARE      = DGLBFLDARE  + B2FLDARE(ISEQ,1)

END DO
!$OMP END PARALLEL DO

END SUBROUTINE CMF_CALC_FLDSTG_DEF
!####################################################################
!
!
!
!
!####################################################################
SUBROUTINE CMF_OPT_FLDSTG_ES
! ==========
! Optional code for Earth Simulator (Vector Processor)
! Specify option: LSTG_ES=.TRUE.
! Faster computation on vector prosessor by avoiding IF-THEN function. (note this code will be slow on Scaler Processor)
! ==========
USE PARKIND1,           ONLY: JPIM, JPRB, JPRD
USE YOS_CMF_INPUT,      ONLY: NLFP
USE YOS_CMF_MAP,        ONLY: NSEQALL, B2GRAREA, B2RIVLEN, B2RIVWTH, B2RIVELV, B2RIVHGT
USE YOS_CMF_MAP,        ONLY: B2RIVSTOMAX, B2FLDSTOMAX, B2FLDGRD, BFRCINC
USE YOS_CMF_PROG,       ONLY: D2RIVSTO, D2FLDSTO
USE YOS_CMF_DIAG,       ONLY: B2RIVDPH, B2FLDDPH, B2FLDFRC, B2FLDARE, B2SFCELV
USE YOS_CMF_DIAG,       ONLY: DGLBSTOPRE2, DGLBSTONEW2, DGLBRIVSTO, DGLBFLDSTO, DGLBFLDARE
IMPLICIT NONE

!*** LOCAL
REAL(KIND=JPRD)            :: B2STODWN(NSEQALL,1)
REAL(KIND=JPRD)            :: D2WTHPRE(NSEQALL,1), D2WTHINC(NSEQALL,1)

! SAVE for OpenMP
INTEGER(KIND=JPIM),SAVE    :: ISEQ, I
REAL(KIND=JPRD),SAVE       :: DSTOALL, DSTONOW, DWTHNOW
!$OMP THREADPRIVATE          (DSTOALL, DSTONOW, DWTHNOW)
!================================================
DGLBRIVSTO=0._JPRD
DGLBFLDSTO=0._JPRD
DGLBFLDARE=0._JPRD

! [1] Assume all waters in river channel
!$OMP PARALLEL DO REDUCTION(+:DGLBSTOPRE2)
DO ISEQ=1, NSEQALL
  DSTOALL = D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)

  D2RIVSTO(ISEQ,1) = DSTOALL
  B2RIVDPH(ISEQ,1) = DSTOALL * B2RIVLEN(ISEQ,1)**(-1.) * B2RIVWTH(ISEQ,1)**(-1.)
  B2RIVDPH(ISEQ,1) = MAX( B2RIVDPH(ISEQ,1), 0._JPRB )
  D2FLDSTO(ISEQ,1) = 0._JPRD
  B2FLDDPH(ISEQ,1) = 0._JPRB
  B2FLDFRC(ISEQ,1) = 0._JPRB
  B2FLDARE(ISEQ,1) = 0._JPRB

  B2STODWN(ISEQ,1) = B2RIVSTOMAX(ISEQ,1)
  D2WTHPRE(ISEQ,1) = B2RIVWTH(ISEQ,1)
  D2WTHINC(ISEQ,1) = B2GRAREA(ISEQ,1) * B2RIVLEN(ISEQ,1)**(-1.) * BFRCINC

  DGLBSTOPRE2     = DGLBSTOPRE2 + DSTOALL
END DO
!$OMP END PARALLEL DO

! [2] Check floodplain level from I=1 to NLFP. Make I-NLFP loop outside for parallel computing (SIMD/Vector)
DO I=1, NLFP

!$OMP PARALLEL DO
  DO ISEQ=1, NSEQALL
    DSTOALL = D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)

    DSTONOW = DSTOALL - B2STODWN(ISEQ,1)
    DSTONOW = MAX( DSTONOW, 0._JPRD )
    DWTHNOW = -D2WTHPRE(ISEQ,1) + &
&    ( D2WTHPRE(ISEQ,1)**2._JPRB + 2._JPRB * DSTONOW * B2RIVLEN(ISEQ,1)**(-1.) * B2FLDGRD(ISEQ,1,I)**(-1.) )**0.5
    DWTHNOW = MIN( DWTHNOW, D2WTHINC(ISEQ,1) )

    B2FLDDPH(ISEQ,1) = B2FLDDPH(ISEQ,1) + B2FLDGRD(ISEQ,1,I) * DWTHNOW
    B2FLDFRC(ISEQ,1) = B2FLDFRC(ISEQ,1) + DWTHNOW/D2WTHINC(ISEQ,1) * NLFP**(-1.)

    !! Update downside floodplain step storage/depth/width 
    B2STODWN(ISEQ,1) = B2FLDSTOMAX(ISEQ,1,I)
    D2WTHPRE(ISEQ,1) = D2WTHPRE(ISEQ,1) + D2WTHINC(ISEQ,1)
  END DO
  !$OMP END PARALLEL DO
END DO

!! [3] flood extent saturated case
!$OMP PARALLEL DO
DO ISEQ=1, NSEQALL
  DSTOALL = D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)
  DSTONOW = DSTOALL - B2STODWN(ISEQ,1)
  DSTONOW = MAX( DSTONOW, 0._JPRD )
  B2FLDDPH(ISEQ,1) = B2FLDDPH(ISEQ,1) + DSTONOW * D2WTHPRE(ISEQ,1)**(-1.) * B2RIVLEN(ISEQ,1)**(-1.)
END DO
!$OMP END PARALLEL DO

!! [4] Floodplain stage diagnose
!$OMP PARALLEL DO
DO ISEQ=1, NSEQALL
  IF( B2FLDDPH(ISEQ,1)>0 )THEN
    DSTOALL = D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)

    B2RIVDPH(ISEQ,1) = B2RIVHGT(ISEQ,1) + B2FLDDPH(ISEQ,1)
    D2RIVSTO(ISEQ,1) = B2RIVLEN(ISEQ,1) * B2RIVWTH(ISEQ,1) * B2RIVDPH(ISEQ,1)
!
    D2FLDSTO(ISEQ,1) = DSTOALL - D2RIVSTO(ISEQ,1)
    D2FLDSTO(ISEQ,1) = MAX( D2FLDSTO(ISEQ,1), 0._JPRD )

    B2FLDFRC(ISEQ,1) = MAX( B2FLDFRC(ISEQ,1),0._JPRB)
    B2FLDFRC(ISEQ,1) = MIN( B2FLDFRC(ISEQ,1),1._JPRB)
    B2FLDARE(ISEQ,1) = B2GRAREA(ISEQ,1)*B2FLDFRC(ISEQ,1)
  ENDIF
END DO
!$OMP END PARALLEL DO

!$OMP PARALLEL DO REDUCTION(+:DGLBSTONEW2,DGLBRIVSTO,DGLBFLDSTO,DGLBFLDARE)
DO ISEQ=1, NSEQALL
  B2SFCELV(ISEQ,1) = B2RIVELV(ISEQ,1) + B2RIVDPH(ISEQ,1)
  DGLBSTONEW2      = DGLBSTONEW2+ D2RIVSTO(ISEQ,1) + D2FLDSTO(ISEQ,1)
  DGLBRIVSTO       = DGLBRIVSTO + D2RIVSTO(ISEQ,1)
  DGLBFLDSTO       = DGLBFLDSTO + D2FLDSTO(ISEQ,1)
  DGLBFLDARE       = DGLBFLDARE + B2FLDARE(ISEQ,1)
END DO
!$OMP END PARALLEL DO

END SUBROUTINE CMF_OPT_FLDSTG_ES
!####################################################################
END MODULE CMF_CALC_FLDSTG_MOD
